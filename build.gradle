plugins {
	id "fabric-loom" version "1.4-SNAPSHOT"
}

version = project.mod_version
group = project.maven_group

repositories {
	maven {
        url "https://api.modrinth.com/maven"
        content { includeGroup "maven.modrinth" }
    }
	maven { url "https://maven.ladysnake.org/releases" } //CCA
	maven { url "https://maven.terraformersmc.com" } //trinkets
	maven { url "https://maven.wispforest.io" } //owo-lib
	maven { url "https://maven.shedaniel.me/" } //cloth-config
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

	modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api}"
	modImplementation "dev.emi:trinkets:${project.trinkets_version}"

	// we don't include these because trinkets already does it
	modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-base:${project.cca_version}"
	modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${project.cca_version}"

	modImplementation include("dev.onyxstudios.cardinal-components-api:cardinal-components-world:${project.cca_version}")

	// use modApi when owo lib is compatible with mc version
	modApi "io.wispforest:owo-lib:${project.owo_lib}"

	modApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config}") { exclude(group: "net.fabricmc.fabric-api") }
	modApi "com.terraformersmc:modmenu:${project.modmenu_version}"

	modLocalRuntime "maven.modrinth:here-be-no-dragons:1.0.0"
	modLocalRuntime "dev.emi:emi-fabric:1.0.29+1.20.4"
}

loom {
	accessWidenerPath = file("src/main/resources/winterly.accesswidener")
}

base {
	archivesName = project.archives_base_name
}

processResources {
	inputs.property "version", project.version
	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 17
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}
